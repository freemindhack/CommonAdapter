apply plugin: 'com.android.library'
apply plugin: 'maven'

final VERSION_NAME = '0.7.3'
final GROUP_ID = 'com.twiceyuan'
final MODULE_ID = 'commonadapter'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 5
        versionName VERSION_NAME
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile "com.android.support:recyclerview-v7:26.1.0"
}

// build a jar with source files
task sourcesJar(type: Jar) {
    //noinspection GroovyAssignabilityCheck
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    //noinspection GroovyAssignabilityCheck
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    //noinspection GroovyAccessibility
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

uploadArchives {
    repositories.mavenDeployer {
        repository(url: REPO_URL) { // 仓库地址
            // 这里配置上传账户的用户名和密码
            authentication(userName: REPO_USERNAME, password: REPO_PASSWORD)
        }
        pom.version = VERSION_NAME
        pom.groupId = GROUP_ID
        pom.artifactId = MODULE_ID
    }
}